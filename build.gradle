plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.1.0'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2021.0.8")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.ewerk.gradle.plugins.querydsl'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // feign api
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

        // swagger
        implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
        implementation 'org.springdoc:springdoc-openapi-javadoc:1.7.0'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // querydsl
        implementation 'com.querydsl:querydsl-jpa:5.0.0'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    // Querydsl 설정부
    def generated = "$buildDir/generated/querydsl"

    querydsl{
        jpa = true
        querydslSourcesDir = generated
    }
    sourceSets{
        main.java.srcDir generated
    }
    configurations{
        querydsl.extendsFrom compileClasspath
    }
    compileQuerydsl{
        options.annotationProcessorPath = configurations.querydsl
    }
    clean {
        delete file(generated)
    }
}
